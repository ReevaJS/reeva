plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.31'
    id 'maven-publish'
}

group = 'com.reevajs'
version = '1.0.0'
def RELEASE = false

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://jitpack.io" }
    maven {
        url "https://repo.mattco.me/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url "https://repo.mattco.me/releases"
        mavenContent {
            releasesOnly()
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1'
    implementation 'com.reevajs:kbgv:e40098e-SNAPSHOT'
    implementation 'com.reevajs:mfbt:1.0.0'
    implementation 'com.reevajs:koffee:8.0.2'
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'org.jruby.joni:joni:2.1.41'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'com.charleskorn.kaml:kaml:0.37.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5:1.6.0'
    testImplementation 'io.strikt:strikt-core:0.32.0'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ["-Xinline-classes", "-Xopt-in=kotlin.RequiresOptIn"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def publishingUsername = null
def publishingPassword = null

if (project.hasProperty("IS_CI")) {
    publishingUsername = System.getenv("REPOSILITE_USER")
    publishingPassword = System.getenv("REPOSILITE_PASSWORD")
} else if (project.hasProperty("reposilite_user")) {
    publishingUsername = project.property("reposilite_user")
    publishingPassword = project.property("reposilite_password")
}

jar {
    from sourceSets.main.output
}

task jarWithSources(type: Jar) {
    classifier('sources')
    from sourceSets.main.allSource
}

if (publishingUsername != null && publishingPassword != null) {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name

                if (RELEASE) {
                    version = project.version
                } else {
                    version = getGitHash()
                }

                artifact jarWithSources

                from components.kotlin
            }
        }

        repositories {
            maven {
                name 'reposilite'
                url = 'https://repo.mattco.me/' + (RELEASE ? 'releases' : 'snapshots')

                credentials {
                    username = publishingUsername
                    password = publishingPassword
                }

                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
